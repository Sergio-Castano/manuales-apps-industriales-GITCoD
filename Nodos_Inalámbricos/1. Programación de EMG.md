Elementos y Conexiones
   -
Redactar los elementos (ESP32, BioAmp Exg Pill, electrodos, cables, protoboard/pcb, bateria) que se necesitan

mas info y conexiones del BioAmp Exg Pill: https://www.crowdsupply.com/upside-down-labs/bioamp-exg-pill

poner diagrama de las conexiones y explcarlas (electrodos tambien)

Poner foto del montaje total (electrodos tambien)


Configuración y Codigo en Arduino
   -
Explicacion de la configuracion con pantallazos:
1. Conectar la ESP32 al PC (Cable USB a Micro USB)
2. Entrar a Arduino IDE y escoger el puerto USB en el que se encuentre la ESP32 en cuestión
3. Seleccionar la placa ESP32 que se este usando, en el caso de no tener las tarjetas de ESP32, seguir este tutorial: [Tutorial ESP32 y Arduino IDE]
4. Cargar el siguiente codigo en un nuevo archivo de arduino, con este codigo se testeará que el BioAmp Exg Pill funcione correctamente
  ```cpp
// EMG Envelop - BioAmp EXG Pill
// https://github.com/upsidedownlabs/BioAmp-EXG-Pill

// Upside Down Labs invests time and resources providing this open source code,
// please support Upside Down Labs and open-source hardware by purchasing
// products from Upside Down Labs!

// Copyright (c) 2021 Upside Down Labs - contact@upsidedownlabs.tech

// Permission is hereby granted, free of charge, to any person obtaining a copy
// of this software and associated documentation files (the "Software"), to deal
// in the Software without restriction, including without limitation the rights
// to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
// copies of the Software, and to permit persons to whom the Software is
// furnished to do so, subject to the following conditions:

// The above copyright notice and this permission notice shall be included in all
// copies or substantial portions of the Software.

// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
// IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
// FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
// AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
// LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
// OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
// SOFTWARE.

#define SAMPLE_RATE 500
#define BAUD_RATE 115200
#define INPUT_PIN 34
#define BUFFER_SIZE 128

int circular_buffer[BUFFER_SIZE];
int data_index, sum;

void setup() {
  Serial.begin(BAUD_RATE);
}

void loop() {
  // Calculate elapsed time
  static unsigned long past = 0;
  unsigned long present = micros();
  unsigned long interval = present - past;
  past = present;

  // Run timer
  static long timer = 0;
  timer -= interval;

  // Sample and get envelop
  if (timer < 0) {
    timer += 1000000 / SAMPLE_RATE;
    int sensor_value = analogRead(INPUT_PIN);
    float signal = EMGFilter(sensor_value);
    int envelop = getEnvelop(abs(signal));
    float envolvente = filtro_pasa_bajos(abs(signal));
    int lim = 1200;
    Serial.print(lim);
    Serial.print(",");
    Serial.print(lim * -1);
    Serial.print(",");
    Serial.print(signal);
    Serial.print(",");
    Serial.println(envolvente*4);
  }
}

// Envelop detection algorithm
int getEnvelop(int abs_emg) {
  sum -= circular_buffer[data_index];
  sum += abs_emg;
  circular_buffer[data_index] = abs_emg;
  data_index = (data_index + 1) % BUFFER_SIZE;
  return (sum / BUFFER_SIZE) * 2;
}

float filtro_pasa_bajos(float entrada) {
  static float Vin, Vin_m_1, Vo_m_1;
  Vin = entrada;
  float Vo = (0.03046 * Vin) + (0.03046 * Vin_m_1) + (0.9391 * Vo_m_1);
  Vin_m_1 = Vin;
  Vo_m_1 = Vo;
  return Vo;
}

// Band-Pass Butterworth IIR digital filter, generated using filter_gen.py.
// Sampling rate: 500.0 Hz, frequency: [74.5, 149.5] Hz.
// Filter is order 4, implemented as second-order sections (biquads).
// Reference:
// https://docs.scipy.org/doc/scipy/reference/generated/scipy.signal.butter.html
// https://courses.ideate.cmu.edu/16-223/f2020/Arduino/FilterDemos/filter_gen.py
float EMGFilter(float input)
{
  float output = input;
  {
    static float z1, z2; // filter section state
    float x = output - 0.05159732 * z1 - 0.36347401 * z2;
    output = 0.01856301 * x + 0.03712602 * z1 + 0.01856301 * z2;
    z2 = z1;
    z1 = x;
  }
  {
    static float z1, z2; // filter section state
    float x = output - -0.53945795 * z1 - 0.39764934 * z2;
    output = 1.00000000 * x + -2.00000000 * z1 + 1.00000000 * z2;
    z2 = z1;
    z1 = x;
  }
  {
    static float z1, z2; // filter section state
    float x = output - 0.47319594 * z1 - 0.70744137 * z2;
    output = 1.00000000 * x + 2.00000000 * z1 + 1.00000000 * z2;
    z2 = z1;
    z1 = x;
  }
  {
    static float z1, z2; // filter section state
    float x = output - -1.00211112 * z1 - 0.74520226 * z2;
    output = 1.00000000 * x + -2.00000000 * z1 + 1.00000000 * z2;
    z2 = z1;
    z1 = x;
  }
  return output;
}
```
Con este codigo corriendo verifica el serial plotter de arduino y se deben ver unas señales de la siguiente manera, la cual debe de variar cada vez que se realiza un estimulo en los electrodos:

![Test_EMG_Signals](/Nodos_Inalámbricos/imgs/Test_EMG_Signals.png)

5. Una vez sabemos que todo esta funcionando correctamente cargamos el siguiente codigo en la ESP32 para que transmita por medio de MQTT los datos:
  ```cpp
#include <WiFi.h>
#include <AsyncMQTT_ESP32.h>
#include <ArduinoJson.hpp>
#include <ArduinoJson.h>
extern "C"
{
#include "freertos/FreeRTOS.h"
#include "freertos/timers.h"
}

#define WIFI_SSID         "************" //CAMBIAR A LA RED LOCAL
#define WIFI_PASSWORD     "************" //CAMBIAR A LA CLAVE DE LA RED LOCAL
#define MQTT_HOST IPAddress(***,***,***,***) //CAMBIAR A LA IP DEL BROKER
#define MQTT_PORT ****  //CAMBIAR AL PUERTO DE COMUNICACIÓN CON EL BROKER

#define client_ID "ESP32_00" //CAMBIAR DEPENDIENDO DE LA NECESIDAD
#define sampled_signal "EMG" //CAMBIAR DEPENDIENDO DE LA NECESIDAD
#define subscription_topic "/start/cmd" //CAMBIAR DEPENDIENDO DE LA NECESIDAD
#define publication_topic "/N1/EMG/data" //CAMBIAR DEPENDIENDO DE LA NECESIDAD

//*-------------------------------------*//
#define sample_time 20 //Periodo de muestreo en ms
#define samples_per_msg 100 //Cantidad de lecturas a empaquetar en un mensaje de tal modo que se transmitan cada 2 segundos
#define queue_size 120 //Tamaño de la pila
#define input_pin 34 //Pin para conectar la señal analógica

int data_sensor[samples_per_msg][queue_size]; //Arreglo donde se escriben y leen las lecturas del sensor
long time_stamp[queue_size]; //Arreglo donde se almacena la estampilla de tiempo de la primera lectura del mensaje
int write_pointer = 0; //Puntero que determina el índice para realizar escritura de datos en la fila FIFO
int send_pointer = 0; //Puntero que determina el índice para realizar la lectura y envio de datos en la fila FIFO
//-----------------------------------------------------------

bool message_published = true;
bool pendingMessage = false;
bool transmit_data = false;
bool ready_to_receive = false;
long publish_count = 0;
float final_envelop;

//***** OBJETOS RTOS *****//
TimerHandle_t mqttReconnectTimer;
TimerHandle_t wifiReconnectTimer;
SemaphoreHandle_t mutexVarCompartida;
TaskHandle_t sampling_task;   
TaskHandle_t EMG_task;

AsyncMqttClient mqttClient;

//************* CALLBACKS ************//
void connectToWifi()
{
  Serial.println("Connecting to Wi-Fi...");
  WiFi.begin(WIFI_SSID, WIFI_PASSWORD);
}

void connectToMqtt()
{
  Serial.println("Connecting to MQTT...");
  mqttClient.connect();
}

void WiFiEvent(WiFiEvent_t event)
{
  Serial.printf("[WiFi-event] event: %d\n", event);
  switch (event)
  {
    case SYSTEM_EVENT_STA_GOT_IP:
      Serial.println("WiFi connected");
      Serial.println("IP address: ");
      Serial.println(WiFi.localIP());
      connectToMqtt();
      break;

    case SYSTEM_EVENT_STA_DISCONNECTED:
      Serial.println("WiFi lost connection");
      xTimerStop(mqttReconnectTimer, 0);
      xTimerStart(wifiReconnectTimer, 0);
      break;

    default:
      break;
  }
}

void onMqttConnect(bool sessionPresent) {
  Serial.println("Connected to MQTT.");
  Serial.print("Session present: ");
  Serial.println(sessionPresent);

  ready_to_receive = false;
  uint16_t packetIdSub = mqttClient.subscribe(subscription_topic, 1);
  Serial.print("Subscribing at QoS 1, packetId: ");
  Serial.println(packetIdSub);
}

void onMqttDisconnect(AsyncMqttClientDisconnectReason reason) {
  String text;
  switch ( reason) {
    case AsyncMqttClientDisconnectReason::TCP_DISCONNECTED:
      text = "TCP_DISCONNECTED";
      break;
    case AsyncMqttClientDisconnectReason::MQTT_UNACCEPTABLE_PROTOCOL_VERSION:
      text = "MQTT_UNACCEPTABLE_PROTOCOL_VERSION";
      break;
    case AsyncMqttClientDisconnectReason::MQTT_IDENTIFIER_REJECTED:
      text = "MQTT_IDENTIFIER_REJECTED";
      break;
    case AsyncMqttClientDisconnectReason::MQTT_SERVER_UNAVAILABLE:
      text = "MQTT_SERVER_UNAVAILABLE";
      break;
    case AsyncMqttClientDisconnectReason::MQTT_MALFORMED_CREDENTIALS:
      text = "MQTT_MALFORMED_CREDENTIALS";
      break;
    case AsyncMqttClientDisconnectReason::MQTT_NOT_AUTHORIZED:
      text = "MQTT_NOT_AUTHORIZED";
      break;
  }
  Serial.printf(" [%8u] Disconnected from the broker reason = %s\n", millis(), text.c_str() );
  Serial.printf(" [%8u] Reconnecting to MQTT..\n", millis());
  if (WiFi.isConnected()) {
    xTimerStart(mqttReconnectTimer, 0);
  }
}

void onMqttSubscribe(const uint16_t& packetId, const uint8_t& qos)
{
  Serial.println("Subscribe acknowledged.");
  Serial.print("  packetId: ");
  Serial.println(packetId);
  Serial.print("  qos: ");
  Serial.println(qos);
  ready_to_receive = true;
}

void onMqttUnsubscribe(const uint16_t& packetId)
{
  Serial.println("Unsubscribe acknowledged.");
  Serial.print("  packetId: ");
  Serial.println(packetId);
}

String GetPayloadContent(char* data, size_t len)
{
  String content = "";
  for (size_t i = 0; i < len; i++)
  {
    content.concat(data[i]);
  }
  return content;
}

void onMqttMessage(char* topic, char* payload, AsyncMqttClientMessageProperties properties, size_t len, size_t index, size_t total) {
  String content = GetPayloadContent(payload, len);
  Serial.println("Publish received.");
  if (content == "start") {
    transmit_data = true; //Inicia la toma de datos
    publish_count = 0;
  }
  else if (content == "stop") {
    transmit_data = false; //Finaliza la toma de datos
    //Reiniciar la toma
  }
}

void onMqttPublish(const uint16_t& packetId)
{
  Serial.println("Publish acknowledged.");
  message_published = true;  // El mensaje ha sido confirmado
  send_pointer++;
  if (send_pointer > queue_size - 1) {
    send_pointer = 0;
  }
}

void setup() {
  Serial.begin(115200);
  analogReadResolution(12);
  pinMode(LED_BUILTIN, OUTPUT);

  while (!Serial && millis() < 5000);

  delay(500);

  Serial.print("\nStarting");

  mqttReconnectTimer = xTimerCreate("mqttTimer", pdMS_TO_TICKS(2000), pdFALSE, (void*)0,
                                    reinterpret_cast<TimerCallbackFunction_t>(connectToMqtt));
  wifiReconnectTimer = xTimerCreate("wifiTimer", pdMS_TO_TICKS(2000), pdFALSE, (void*)0,
                                    reinterpret_cast<TimerCallbackFunction_t>(connectToWifi));
  mutexVarCompartida = xSemaphoreCreateMutex();

  WiFi.onEvent(WiFiEvent);

  mqttClient.onConnect(onMqttConnect);
  mqttClient.onDisconnect(onMqttDisconnect);
  mqttClient.onSubscribe(onMqttSubscribe);
  mqttClient.onUnsubscribe(onMqttUnsubscribe);
  mqttClient.onMessage(onMqttMessage);
  mqttClient.onPublish(onMqttPublish);

  mqttClient.setKeepAlive(30);
  mqttClient.setCleanSession(false);
  mqttClient.setClientId(client_ID);
  mqttClient.setServer(MQTT_HOST, MQTT_PORT);

  connectToWifi();

  while (WiFi.status() != WL_CONNECTED) {
    delay(500);
    Serial.print(".");
  }

  Serial.println("");
  Serial.println("WiFi connected");
  Serial.println("IP address: ");
  Serial.println(WiFi.localIP());

  delay(500);
  
  connectToMqtt();

  xTaskCreatePinnedToCore(
    sample_Loop, /* Funcion de la tarea */   //CAMBIAR POR emg_loop en el otro codigo
    "sample_loop", /* Nombre de la tarea */
    10000,  /* Tamaño de la pila */
    NULL,  /* Parametros de entrada */
    configMAX_PRIORITIES - 1, /* Prioridad de la tarea */
    NULL,  /* objeto TaskHandle_t. */
    0); /* Nucleo donde se correra */

  xTaskCreatePinnedToCore(
    publishData, /* Funcion de la tarea */
    "publish_data", /* Nombre de la tarea */
    10000,  /* Tamaño de la pila */
    NULL,  /* Parametros de entrada */
    configMAX_PRIORITIES - 10, /* Prioridad de la tarea */
    NULL,  /* objeto TaskHandle_t. */
    1); /* Nucleo donde se correra */

  vTaskSuspend(NULL);
}

void Send_MQTT_msg(int pointer)
{
  String json;

  DynamicJsonDocument doc(16384);

  doc["sensor"] = sampled_signal;
  doc["sample"] = sample_time;
  doc["index"] = time_stamp[pointer];

  JsonArray data = doc.createNestedArray("data");
  for (int i = 0; i < samples_per_msg; i = i + 1) {
    data.add(data_sensor[i][pointer]);
  }

  serializeJson(doc, json);

  mqttClient.publish(publication_topic, 1, false, (char*)json.c_str());  //QoS 1
}

void sample_Loop (void * pvParameters) {
  Serial.println("Muestreo se ejecuta en el nucleo: ");
  Serial.println(xPortGetCoreID());

  TickType_t xLastWakeTime;
  const TickType_t xFrequency = sample_time;

  int i = 0;
  int count = 0;
  bool arreglo_completo = true;
  bool in_progress = false;

  // Initialise the xLastWakeTime variable with the current time.
  xLastWakeTime = xTaskGetTickCount();

  for ( ;; ) {
    int sensor_value = analogRead(input_pin);
    float signal = EMGFilter(sensor_value);
    float envelop = low_pass_filter(abs(signal));
    
    final_envelop = envelop * 4;

    if (transmit_data == true || in_progress == true) {
      in_progress = true;
      data_sensor[i][write_pointer] = final_envelop;
      i++;
      if (i >= samples_per_msg) {
        i = 0;
        in_progress = false;
        time_stamp[write_pointer] = count;
        count++;
        xSemaphoreTake(mutexVarCompartida, portMAX_DELAY);
        write_pointer++;
        if (write_pointer > queue_size - 1) {
          write_pointer = 0;
        }
        xSemaphoreGive(mutexVarCompartida);
      }
    }
    else {
      count = 0;
    }
    // Wait for the next cycle.
    vTaskDelayUntil( &xLastWakeTime, xFrequency )
  }
}

void publishData (void * pvParameters) {
  Serial.println("Envio de datos se ejecuta en el nucleo: ");
  Serial.println(xPortGetCoreID());

  TickType_t xLastWakeTime;
  const TickType_t xFrequency = 100;

  int auxiliar_pointer;

  // Initialise the xLastWakeTime variable with the current time.
  xLastWakeTime = xTaskGetTickCount();

  for ( ;; ) {
    if (mqttClient.connected() && ready_to_receive) {
      digitalWrite(LED_BUILTIN, HIGH);
      if (message_published)
      {
        xSemaphoreTake(mutexVarCompartida, portMAX_DELAY);
        auxiliar_pointer = write_pointer;
        xSemaphoreGive(mutexVarCompartida);

        if (send_pointer != auxiliar_pointer) {
          publish_count++;
          Serial.printf("Publishing %d\n", publish_count);
          message_published = false; 
          Send_MQTT_msg(send_pointer);
        }
      }

    }
    else {
      digitalWrite(LED_BUILTIN, LOW);
    }
    // Wait for the next cycle.
    vTaskDelayUntil( &xLastWakeTime, xFrequency )
  }
}
//---------------EMG-----------------------------------------------------------------------
// Band-Pass Butterworth IIR digital filter, generated using filter_gen.py.
// Sampling rate: 500.0 Hz, frequency: [74.5, 149.5] Hz.
// Filter is order 4, implemented as second-order sections (biquads).
float EMGFilter(float input)
{
  float output = input;
  {
    static float z1, z2; // filter section state
    float x = output - 0.05159732 * z1 - 0.36347401 * z2;
    output = 0.01856301 * x + 0.03712602 * z1 + 0.01856301 * z2;
    z2 = z1;
    z1 = x;
  }
  {
    static float z1, z2; // filter section state
    float x = output - -0.53945795 * z1 - 0.39764934 * z2;
    output = 1.00000000 * x + -2.00000000 * z1 + 1.00000000 * z2;
    z2 = z1;
    z1 = x;
  }
  {
    static float z1, z2; // filter section state
    float x = output - 0.47319594 * z1 - 0.70744137 * z2;
    output = 1.00000000 * x + 2.00000000 * z1 + 1.00000000 * z2;
    z2 = z1;
    z1 = x;
  }
  {
    static float z1, z2; // filter section state
    float x = output - -1.00211112 * z1 - 0.74520226 * z2;
    output = 1.00000000 * x + -2.00000000 * z1 + 1.00000000 * z2;
    z2 = z1;
    z1 = x;
  }
  return output;
}

float low_pass_filter(float Data_in) {
  static float Vin, Vin_m_1, Vo_m_1;
  Vin = Data_in;
  float Vo = (0.03046 * Vin) + (0.03046 * Vin_m_1) + (0.9391 * Vo_m_1);
  Vin_m_1 = Vin;
  Vo_m_1 = Vo;
  return Vo;
}
//--------------------------------------------------------------------------------------
void loop() {
  vTaskSuspend(NULL);
}
```
  Recordar cambiar algunos parametros iniciales como la IP del broker, el topic a publicar, el topic a suscribirse, la red wifi local a conectarse y el ID del cliente ante el broker

  Listo el EMG esta transmitiendo por medio de MQTT una vez reciba la señal!








  [Tutorial ESP32 y Arduino IDE]: https://randomnerdtutorials.com/installing-the-esp32-board-in-arduino-ide-windows-instructions/
