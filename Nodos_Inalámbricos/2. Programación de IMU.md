Elementos y Conexiones (FALTA)
   -
Para realizar la implementación de los sensores inerciales (IMU) se necesitará un ESP32, un microcontrolador con conectividad Wi-Fi y Bluetooth; un sensor Inercial LSM6DSOX; cables de conexion y otros componentes; una protoboard/PCB para montar y conectar los componentes del circuito; y una batería para alimentar el sistema. 

Tener en cuenta que estos materiales son para cada uno de los nodos inerciales, para este caso se necesitan 2, uno en la muñeca y otro en el antebrazo con el fin de medir el angulo entre la muñeca y el antebrazo.

A continuación se muestra una foto de todos los componentes, con sus respectivas conexiones
<img src="/Nodos_Inalámbricos/imgs/IMU.JPG" width="900px">
<img src="/Nodos_Inalámbricos/imgs/NodosIMU_bb.png" width="900px">


Configuración y Codigo en Arduino
   -
1. Conectar la ESP32 al PC (Cable USB a Micro USB)
2. Entrar a Arduino IDE y escoger el puerto USB en el que se encuentre la ESP32 en cuestión
3. Seleccionar la placa ESP32 que se este usando, en el caso de no tener las tarjetas de ESP32, seguir este tutorial: [Tutorial ESP32 y Arduino IDE]
4. cargar el siguiente codigo en un nuevo archivo de arduino para que transmita por medio de MQTT los datos:
  ```cpp
#include <WiFi.h>
extern "C" {
#include "freertos/FreeRTOS.h"
#include "freertos/timers.h"
}
#include <AsyncMQTT_ESP32.h>
#include <ArduinoJson.hpp>
#include <ArduinoJson.h>

#define WIFI_SSID         "************" //CAMBIAR A LA RED LOCAL
#define WIFI_PASSWORD     "************" //CAMBIAR A LA CLAVE DE LA RED LOCAL
#define MQTT_HOST IPAddress(***,***,***,***) //CAMBIAR A LA IP DEL BROKER
#define MQTT_PORT ****  //CAMBIAR AL PUERTO DE COMUNICACIÓN CON EL BROKER

#define client_ID "ESP32_107"                   //CAMBIAR A "ESP32_108" PARA EL OTRO SENSOR INERCIAL (CAMBIAR SEGUN LA NECESIDAD)
#define subscription_topic "/ESP32/cmd"         //CAMBIAR SEGUN LA NECESIDAD
#define publication_topic "/ESP32_N7/data"      //CAMBIAR A "/ESP32_N8/data"  PARA EL OTRO SENSOR INERCIAL (CAMBIAR SEGUN LA NECESIDAD)

#include <Adafruit_LSM6DSOX.h>
Adafruit_LSM6DSOX sox;

#define LSM_CS 5
#define LSM_SCK 18
#define LSM_MISO 19
#define LSM_MOSI 23

double offset_gyro_X = 0.0055;  //CALIBRAR
double offset_gyro_Y = -0.0037; //CALIBRAR
double offset_gyro_Z = -0.0092; //CALIBRAR

bool transmit_data = false;
bool ready_to_receive = false;
bool message_published = true;
bool hubo_reconexion = false;
long publish_count = 0;
long contador_ack = 0;
int cont_aux = 0;
unsigned long orden = 0;
uint16_t last_pub = 0;
uint16_t last_ack = -1;

AsyncMqttClient mqttClient;

//***** OBJETOS RTOS *****//
TaskHandle_t sampling_task;
TimerHandle_t mqttReconnectTimer;
TimerHandle_t wifiReconnectTimer;
SemaphoreHandle_t mutexVarCompartida;

#define sample_time 10 
#define samples_per_msg 100
#define queue_size 16
#define inputPin 34

double ax[samples_per_msg][queue_size];
double ay[samples_per_msg][queue_size]; 
double az[samples_per_msg][queue_size]; 
double gx[samples_per_msg][queue_size]; 
double gy[samples_per_msg][queue_size]; 
double gz[samples_per_msg][queue_size]; 

double data_sensor[samples_per_msg][queue_size]; 
long intervalo_tiempo[queue_size]; //Arreglo donde se escribe el tiempo en ms de la primera lectura

int write_pointer = 0; //Puntero que determina el índice para realizar escritura de datos en la fila FIFO
int send_pointer = 0; //Puntero que determina el índice para realizar la lectura y envio de datos en la fila FIFO

void connectToWifi() {
  Serial.println("Connecting to Wi-Fi...");
  WiFi.begin(WIFI_SSID, WIFI_PASSWORD);
}

void connectToMqtt() {
  Serial.println("Connecting to MQTT...");
  mqttClient.connect();
}

void WiFiEvent(WiFiEvent_t event) {
  Serial.printf("[WiFi-event] event: %d\n", event);
  switch (event) {
    case SYSTEM_EVENT_STA_GOT_IP:
      Serial.println("WiFi connected");
      Serial.println("IP address: ");
      Serial.println(WiFi.localIP());
      connectToMqtt();
      break;
    case SYSTEM_EVENT_STA_DISCONNECTED:
      Serial.println("WiFi lost connection");
      xTimerStop(mqttReconnectTimer, 0);
      xTimerStart(wifiReconnectTimer, 0);
      break;
  }
}

void onMqttConnect(bool sessionPresent) {
  Serial.println("Connected to MQTT.");
  Serial.print("Session present: ");
  Serial.println(sessionPresent);
  ready_to_receive = false;
  uint16_t packetIdSub = mqttClient.subscribe(subscription_topic, 0);
  Serial.print("Subscribing at QoS 0, packetId: ");
  Serial.println(packetIdSub);
}

void onMqttDisconnect(AsyncMqttClientDisconnectReason reason) {
  String text;
  switch ( reason) {
    case AsyncMqttClientDisconnectReason::TCP_DISCONNECTED:
      text = "TCP_DISCONNECTED";
      break;
    case AsyncMqttClientDisconnectReason::MQTT_UNACCEPTABLE_PROTOCOL_VERSION:
      text = "MQTT_UNACCEPTABLE_PROTOCOL_VERSION";
      break;
    case AsyncMqttClientDisconnectReason::MQTT_IDENTIFIER_REJECTED:
      text = "MQTT_IDENTIFIER_REJECTED";
      break;
    case AsyncMqttClientDisconnectReason::MQTT_SERVER_UNAVAILABLE:
      text = "MQTT_SERVER_UNAVAILABLE";
      break;
    case AsyncMqttClientDisconnectReason::MQTT_MALFORMED_CREDENTIALS:
      text = "MQTT_MALFORMED_CREDENTIALS";
      break;
    case AsyncMqttClientDisconnectReason::MQTT_NOT_AUTHORIZED:
      text = "MQTT_NOT_AUTHORIZED";
      break;
  }
  Serial.printf(" [%8u] Disconnected from the broker reason = %s\n", millis(), text.c_str() );
  Serial.printf(" [%8u] Reconnecting to MQTT..\n", millis());
  if (WiFi.isConnected()) {
    xTimerStart(mqttReconnectTimer, 0);
  }
}

void onMqttSubscribe(uint16_t packetId, uint8_t qos) {
  Serial.println("Subscribe acknowledged.");
  Serial.print("  packetId: ");
  Serial.println(packetId);
  Serial.print("  qos: ");
  Serial.println(qos);
  ready_to_receive = true;
}

void onMqttUnsubscribe(uint16_t packetId) {
  Serial.println("Unsubscribe acknowledged.");
  Serial.print("  packetId: ");
  Serial.println(packetId);
}

String GetPayloadContent(char* data, size_t len)
{
  String content = "";
  for (size_t i = 0; i < len; i++)
  {
    content.concat(data[i]);
  }
  return content;
}

void onMqttMessage(char* topic, char* payload, AsyncMqttClientMessageProperties properties, size_t len, size_t index, size_t total) {
  String content = GetPayloadContent(payload, len);
  if (content == "start") {
    transmit_data = true; //Inicia la toma de datos
  }
  else if (content == "stop") {
    transmit_data = false; //Finaliza la toma de datos
  }
}

void onMqttPublish(uint16_t packetId) {
  Serial.println("Publish acknowledged.");
  message_published = true;  // El mensaje ha sido confirmado
  send_pointer++;
  if (send_pointer > queue_size - 1) {
    send_pointer = 0;
  }
  
}

void imu_setup() {
  if (!sox.begin_SPI(LSM_CS, LSM_SCK, LSM_MISO, LSM_MOSI)) {
    Serial.println("Failed to find LSM6DSOX chip");
    while (1) {
      delay(10);
    }
  }
  Serial.println("LSM6DSOX Found!");
  sox.setAccelRange(LSM6DS_ACCEL_RANGE_8_G);
  sox.setGyroRange(LSM6DS_GYRO_RANGE_500_DPS );
  sox.setAccelDataRate(LSM6DS_RATE_416_HZ);
  sox.setGyroDataRate(LSM6DS_RATE_416_HZ);
}

void setup() {
  Serial.begin(115200);
  analogReadResolution(12);
  pinMode(LED_BUILTIN, OUTPUT);

  while (!Serial && millis() < 5000);

  delay(500);

  Serial.print("\nStarting");

  mqttReconnectTimer = xTimerCreate("mqttTimer", pdMS_TO_TICKS(2000), pdFALSE, (void*)0,
                                    reinterpret_cast<TimerCallbackFunction_t>(connectToMqtt));
  wifiReconnectTimer = xTimerCreate("wifiTimer", pdMS_TO_TICKS(2000), pdFALSE, (void*)0,
                                    reinterpret_cast<TimerCallbackFunction_t>(connectToWifi));
  mutexVarCompartida = xSemaphoreCreateMutex();

  WiFi.onEvent(WiFiEvent);

  mqttClient.onConnect(onMqttConnect);
  mqttClient.onDisconnect(onMqttDisconnect);
  mqttClient.onSubscribe(onMqttSubscribe);
  mqttClient.onUnsubscribe(onMqttUnsubscribe);
  mqttClient.onMessage(onMqttMessage);
  mqttClient.onPublish(onMqttPublish);

  mqttClient.setKeepAlive(30);
  mqttClient.setCleanSession(false);
  mqttClient.setClientId(client_ID);
  mqttClient.setServer(MQTT_HOST, MQTT_PORT);

  connectToWifi();

  imu_setup();

  xTaskCreatePinnedToCore(
    sampling_loop, /* Funcion de la tarea1 */
    "sampling_task", /* Nombre de la tarea */
    10000,  /* Tamaño de la pila */
    NULL,  /* Parametros de entrada */
    10,  /* Prioridad de la tarea */
    &sampling_task,  /* objeto TaskHandle_t. */
    0); /* Nucleo donde se correra */

  xTaskCreatePinnedToCore(
    publishData, /* Funcion de la tarea */
    "publish_data", /* Nombre de la tarea */
    10000,  /* Tamaño de la pila */
    NULL,  /* Parametros de entrada */
    configMAX_PRIORITIES - 10, /* Prioridad de la tarea */
    NULL,  /* objeto TaskHandle_t. */
    1); /* Nucleo donde se correra */
}

void Send_MQTT_msg(int pointer)
{
  String json;
  DynamicJsonDocument doc(20000);

  doc["sensor"] = "IMU";
  doc["sample"] = sample_time;
  doc["t0"] = intervalo_tiempo[pointer];

  JsonArray data1 = doc.createNestedArray("ax");
  for (int i = 0; i < samples_per_msg; i = i + 1) {
    data1.add(ax[i][pointer]);
  }

  JsonArray data2 = doc.createNestedArray("ay");
  for (int i = 0; i < samples_per_msg; i = i + 1) {
    data2.add(ay[i][pointer]);
  }

  JsonArray data3 = doc.createNestedArray("az");
  for (int i = 0; i < samples_per_msg; i = i + 1) {
    data3.add(az[i][pointer]);
  }

  JsonArray data4 = doc.createNestedArray("gx");
  for (int i = 0; i < samples_per_msg; i = i + 1) {
    data4.add(gx[i][pointer]);
  }

  JsonArray data5 = doc.createNestedArray("gy");
  for (int i = 0; i < samples_per_msg; i = i + 1) {
    data5.add(gy[i][pointer]);
  }

  JsonArray data6 = doc.createNestedArray("gz");
  for (int i = 0; i < samples_per_msg; i = i + 1) {
    data6.add(gz[i][pointer]);
  }
  
  serializeJson(doc, json);

  uint16_t packetIdPub1 = mqttClient.publish(publication_topic, 2, false, (char*)json.c_str());;
  last_pub = packetIdPub1;
}

void sampling_loop(void * pvParameters)
{
  Serial.print("Tarea1 se corre en el nucleo: ");
  Serial.println(xPortGetCoreID());

  int i = 0;
  bool arreglo_completo = true;
  bool continuar = true;

  TickType_t xLastWakeTime;
  const TickType_t xFrequency = sample_time;

  // Initialise the xLastWakeTime variable with the current time.
  xLastWakeTime = xTaskGetTickCount();

  for ( ;; )
  {
    // Wait for the next cycle.
    vTaskDelayUntil( &xLastWakeTime, xFrequency )

    if ((transmit_data == true) || (arreglo_completo == false)) {
      continuar = true;
    }
    else {
      continuar = false;
      i = 0;
    }

    if (continuar == true) {
      // Perform action here.
      sensors_event_t accel;
      sensors_event_t gyro;
      sensors_event_t temp;
      sox.getEvent(&accel, &gyro, &temp);      
      
      ax[i][write_pointer] = accel.acceleration.x;
      ay[i][write_pointer] = accel.acceleration.y;
      az[i][write_pointer] = accel.acceleration.z;
      gx[i][write_pointer] = gyro.gyro.x - offset_gyro_X;
      gy[i][write_pointer] = gyro.gyro.y - offset_gyro_Y;
      gz[i][write_pointer] = gyro.gyro.z - offset_gyro_Z;
      
      if (i == 0) {
        intervalo_tiempo[write_pointer] = orden;
        orden++;
      }
      i++;

      if (i >= samples_per_msg) {
        i = 0;
        arreglo_completo = true;
        write_pointer = write_pointer + 1;
        if (write_pointer > queue_size - 1) {
          write_pointer = 0;
        }
      }
      else {
        arreglo_completo = false;
      }
    }
  }
}

void publishData (void * pvParameters) {

  Serial.print("Envio de datos se ejecuta en el nucleo: ");
  Serial.println(xPortGetCoreID());

  TickType_t xLastWakeTime;
  const TickType_t xFrequency = 100;

  int auxiliar_pointer;

  // Initialise the xLastWakeTime variable with the current time.
  xLastWakeTime = xTaskGetTickCount();

  for ( ;; ) {
    if (mqttClient.connected() && ready_to_receive) {
      digitalWrite(LED_BUILTIN, HIGH);
      if (message_published)
      {
        xSemaphoreTake(mutexVarCompartida, portMAX_DELAY);
        auxiliar_pointer = write_pointer;
        xSemaphoreGive(mutexVarCompartida);

        if (send_pointer != auxiliar_pointer) {
          publish_count++;
          Serial.printf("Publishing %d\n", publish_count);
          message_published = false;
          Send_MQTT_msg(send_pointer);
        }
      }
    }
    else {
      digitalWrite(LED_BUILTIN, LOW);
    }
    // Wait for the next cycle.
    vTaskDelayUntil( &xLastWakeTime, xFrequency )
  }
}

void loop() {
  vTaskSuspend(NULL);
}
```
Recordar cambiar algunos parametros iniciales como la IP del broker, el topic a publicar, el topic a suscribirse, la red wifi local a conectarse y el ID del cliente ante el broker

5. Hacer el mismo proceso para el segundo sensor inercial pero cambiando el topic a publicar y el ID del cliente ante el broker especificamente



  Listo las IMUS estan transmitiendo por medio de MQTT, una vez que reciban la señal!







 [Tutorial ESP32 y Arduino IDE]: https://randomnerdtutorials.com/installing-the-esp32-board-in-arduino-ide-windows-instructions/
